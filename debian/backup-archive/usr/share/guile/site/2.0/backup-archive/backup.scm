(define-module (backup-archive backup)
  #:use-module (backup-archive program)
  #:use-module (backup-archive message)
  #:use-module (backup-archive common)
  #:use-module (backup-archive config)
  #:use-module (backup-archive profile)
  #:use-module (backup-archive archive)
  #:use-module (backup-archive find)
  #:export     (make-backup))

(define (make-backup id)
  (let* ((profile (and (format #t "loading profile ~a...\n" id)
                       (load-profile (string->symbol id))))
         (fmt (profile-fmt profile))
         (archive (and (display "searching archives...\n")
                       (archive-find id (profile-dest profile) fmt)))
         (find-status (and (format #t "generating file list...\n")
                           (find-files (profile-orig profile)
                                       (profile-pred profile)
                                       (car archive)))))
    (cond
     ((not (zero? (cadr find-status)))
      (msg-error "find exited with non-zero status")
      (exit 1))
     (else
      (format #t "file count: ~s\n" (string-count (car find-status) #\null))
      (format #t "output file: ~a\n" (cdr archive))
      (if (ask-yes-no-question? "continue? ")
          (let* ((files-from-filename (string-copy "/tmp/BA.XXXXXX"))
                 (files-from-port (mkstemp! files-from-filename)))
            (catch #t
                   (lambda ()
                     (display (car find-status) files-from-port)
                     (archive-write (cdr archive)
                                    files-from-filename
                                    (archive-format-filters fmt))
                     (close-port files-from-port)
                     (delete-file files-from-filename)
                     (format #t "done.\n"))
                   (lambda (key . rest)
                     (close-port files-from-port)
                     (delete-file files-from-filename)
                     (msg-error "~a" key))))
          (format #t "aborting.\n"))))))

